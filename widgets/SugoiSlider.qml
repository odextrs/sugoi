import QtQuick
import qs.config

//from: https://github.com/Guilherme4Colamarco/kamalen-shell/blob/main/.config/quickshell/components/ConfigSlider.qml
Item {
    id: root

    property real value: 0
    property real minValue: 0
    property real maxValue: 100
    property real stepSize: 1
    property real currentValue: root.value
    signal valueModified(real v)

    height: 10
    width: parent.width
    onValueChanged: root.currentValue = root.value

    function _snap(v) {
        if (root.stepSize <= 0) return v
        return Math.round(v / root.stepSize) * root.stepSize
    }

    function _valueFromX(x) {
        var t = Math.max(0, Math.min(1, x / trackArea.width))
        return root._snap(root.minValue + t * (root.maxValue - root.minValue))
    }

    function _format(v) {
        var rounded = Math.round(v)
        if (Math.abs(v - rounded) < 0.0001) return rounded.toString()
        return v.toFixed(2)
    }

    Item {
        id: trackArea
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.top: labelText.bottom
        anchors.topMargin: 10
        height: 6

        Rectangle {
            anchors.fill: parent
            radius: height / 2
            color: Colour.surfaceVariant
        }

        Rectangle {
            width: {
                var range = root.maxValue - root.minValue
                if (range <= 0) return 0
                return Math.max(0, Math.min(parent.width,
                    parent.width * (root.currentValue - root.minValue) / range))
            }
            height: parent.height
            radius: height / 2
            color: Colour.primary
        }

        Rectangle {
            x: {
                var range = root.maxValue - root.minValue
                if (range <= 0) return 0
                return Math.max(0, Math.min(parent.width - width,
                    parent.width * (root.currentValue - root.minValue) / range))
            }
            anchors.verticalCenter: parent.verticalCenter
            width: 14
            height: 14
            radius: 7
            color: Colour.primary
            scale: sliderMa.containsMouse || sliderMa.pressed ? 1.2 : 1.0

            Behavior on scale {
                NumberAnimation { duration: Animations.snap; easing.type: Easing.OutBack; easing.overshoot: Animations.springPower }
            }
        }

        MouseArea {
            id: sliderMa
            anchors.fill: parent
            anchors.margins: -8
            hoverEnabled: true
            cursorShape: Qt.PointingHandCursor
            preventStealing: true

            onPressed: mouse => root.currentValue = root._valueFromX(mouse.x)
            onPositionChanged: mouse => { if (pressed) root.currentValue = root._valueFromX(mouse.x) }
            onReleased: root.valueModified(root.currentValue)
        }
    }
}